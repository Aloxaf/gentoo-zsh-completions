#autoload

# Usage:
#   _gentoo_repos    -> returns the main repo (with PORTDIR fallback)
#   _gentoo_repos -o -> returns the list of non-main repos (with PORTDIR_OVERLAY fallback)

_gentoo_repos() {
  local main_repo main_repo_path overlay overlay_path

  if [[ -e /usr/share/portage/config/repos.conf ]]; then
    if [[ ${1} == "-o" ]]; then
      for overlay in $(_repos_conf -l); do
        overlay_path+=($(_repos_conf ${overlay} location))
      done

      source /etc/make.conf 2>/dev/null
      source /etc/portage/make.conf 2>/dev/null

      overlay_path+=(${(@)PORTDIR_OVERLAY})

      echo "${(@u)overlay_path}"
    else
      main_repo=$(_repos_conf DEFAULT main-repo)
      main_repo_path=$(_repos_conf ${main_repo} location)

      echo "${main_repo_path}"
    fi
  else
    source /usr/share/portage/config/make.globals 2>/dev/null
    source /etc/make.conf 2>/dev/null
    source /etc/portage/make.conf 2>/dev/null

    if [[ ${1} == "-o" ]]; then
      echo "${(@u)PORTDIR_OVERLAY}"
    else
      echo "${PORTDIR}"
    fi
  fi
}

_repos_conf() {
  local v file insection section arr

  for file in /usr/share/portage/config/repos.conf \
           /etc/portage/repos.conf \
           /etc/portage/repos.conf/*.conf; do

    [[ -f ${file} ]] || continue
    insection=0
    declare -A arr
    IFS='= '

    while read -r name value; do
      [[ -z ${name} || ${name} == '#'* ]] && continue

      if [[ (${name} == '['*']') && (-z ${value}) ]]; then
        value=${name//(\]|\[)}
        name="section"
      fi
      arr[${name}]=${value}

      if [[ ${insection} == 1 && ${name} == "section" ]]; then
        break
      elif [[ ${name} == "section" ]]; then
        [[ ${value} == ${1} ]] && insection=1
        secname+=(${value})
      elif [[ ${insection} == 1 ]]; then
        if [[ ${name} == ${2} ]]; then
          v=${value}
        fi
      fi
      continue
    done < ${file}
  done

  if [[ ${1} == "-l" ]]; then
    echo "${(@)secname}"
  else
    echo "${v}"
  fi
}

_gentoo_repos "$@"

# vim: set et sw=2 ts=2 ft=zsh:
